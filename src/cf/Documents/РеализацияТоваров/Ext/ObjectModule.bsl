
Процедура ОбработкаПроведения(Отказ, Режим)
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("Состояние") И (ЭтотОбъект.ДополнительныеСвойства.Состояние = "РасчетСебестоимости") Тогда
		РассчитатьСебестоимость(Отказ);
	Иначе
		// Если не подает, то просто списываем количество
		СписатьКоличество(Отказ);
		// Если никаких проблем со списанием не возникло, то ...
		Если Не Отказ Тогда
			Движения.СтоимостьТоваров.Записывать = Истина;
			НачалоРасчетаСебестоимости = Константы.НачалоРасчетаСебестоимости.Получить();
			Если НачалоРасчетаСебестоимости = Дата(1, 1, 1) Или (НачалоРасчетаСебестоимости > МоментВремени().Дата) Тогда
				Константы.НачалоРасчетаСебестоимости.Установить(МоментВремени().Дата);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура СписатьКоличество(Отказ)
	// Формирую движения
	Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.ОстаткиТоваров.Очистить();
	Движения.ОстаткиТоваров.БлокироватьДляИзменения = Истина;
	
	Для каждого СтрТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = СтрТовары.Номенклатура;
		Движение.Количество = СтрТовары.Количество;
	КонецЦикла;
	
	// Записываю движения и проверяю остаток
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровТовары.Номенклатура,
		|	МИНИМУМ(РеализацияТоваровТовары.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ втТоварыДокумента
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТоварыДокумента.НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕ(ОстаткиТоваровОстатки.Номенклатура) КАК НоменклатураПредставление,
		|	ОстаткиТоваровОстатки.КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиТоваров.Остатки(
		|			&Период,
		|			Номенклатура В
		|				(ВЫБРАТЬ
		|					втТоварыДокумента.Номенклатура
		|				ИЗ
		|					втТоварыДокумента КАК втТоварыДокумента)) КАК ОстаткиТоваровОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ втТоварыДокумента КАК втТоварыДокумента
		|		ПО ОстаткиТоваровОстатки.Номенклатура = втТоварыДокумента.Номенклатура
		|ГДЕ
		|	ОстаткиТоваровОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		Отказ = Истина;
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает " + Выборка.КоличествоОстаток + " номенклатуры """ + Выборка.НоменклатураПредставление + """ в строке " + Выборка.НомерСтроки;
			Сообщение.Поле = "Товары[" + Строка(Выборка.НомерСтроки - 1) + "].Количество";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить(); 
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры
 
Процедура РассчитатьСебестоимость(Отказ)
	Движения.СтоимостьТоваров.Записывать = Истина;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СтоимостьТоваров");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = Товары;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализацияТоваровТовары.Номенклатура,
		|	СУММА(РеализацияТоваровТовары.Количество) КАК Количество,
		|	СУММА(РеализацияТоваровТовары.Сумма) КАК Сумма
		|ПОМЕСТИТЬ втТоварыДокумента
		|ИЗ
		|	Документ.РеализацияТоваров.Товары КАК РеализацияТоваровТовары
		|ГДЕ
		|	РеализацияТоваровТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТоваровТовары.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТоварыДокумента.Номенклатура КАК Номенклатура,
		|	втТоварыДокумента.Количество КАК Количество,
		|	втТоварыДокумента.Сумма КАК Сумма,
		|	СтоимостьТоваровОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(СтоимостьТоваровОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(СтоимостьТоваровОстатки.СтоимостьОстаток, 0) КАК СтоимостьОстаток
		|ИЗ
		|	втТоварыДокумента КАК втТоварыДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СтоимостьТоваров.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						втТоварыДокумента.Номенклатура
		|					ИЗ
		|						втТоварыДокумента)) КАК СтоимостьТоваровОстатки
		|		ПО втТоварыДокумента.Номенклатура = СтоимостьТоваровОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	СтоимостьТоваровОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Блокировка.Заблокировать();
	ВыборкаНоменклатура = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.Количество > ВыборкаНоменклатура.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает товара """ + ВыборкаНоменклатура.Номенклатура + """! Требуется: " 
				+ ВыборкаНоменклатура.Количество + ", остаток: " + ВыборкаНоменклатура.КоличествоОстаток + ".";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		СтоимостьВСЕГО = 0;
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		Выборка = ВыборкаНоменклатура.Выбрать();
		Пока (ОсталосьСписать > 0) И Выборка.Следующий() Цикл
			// регистр СтоимостьНоменклатуры Расход
			Движение = Движения.СтоимостьТоваров.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Партия = Выборка.Партия;
			Движение.Количество = Мин(ОсталосьСписать, Выборка.КоличествоОстаток);
			Движение.Стоимость = (Движение.Количество * Выборка.СтоимостьОстаток) / Выборка.КоличествоОстаток;
			
			СтоимостьВСЕГО = СтоимостьВСЕГО + Движение.Стоимость;
			ОсталосьСписать = ОсталосьСписать - Движение.Количество;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры
 